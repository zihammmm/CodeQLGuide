# 查询
查询是一个QL程序的输出。它们求出一系列的结果。  

有两类查询。对于一个给定的查询模型，该模型中的查询是：  
+ 模块中定义的select子句(如果有的话)
+ 该模块的谓词名空间(namspace)中的任意查询谓词。也就是说，它们可以在模块本身中被定义，或者从一个不同的模块引入。  

我们也经常将整个QL程序称为一个查询。  

## select子句(Select clauses)
当编写一个查询模块时，你可以以下面的格式包含一个select子句(通常是在文件末尾)：  
```ql
from /* ... variable declarations ... */
where /* ... logical formula ... */
select /* ... expressions ... */
```
*from* 和 *where* 部分是可选的。  

除了表达式，你也可以包含：  
+ *as* 关键字，后面跟着一个名称。这为结果的一列提供一个“标签”，并且允许你在后续的select表达式中使用它们。  
+ *order by* 关键字，后面跟着结果的列名，还有可选的关键字 *asc* 或者 *desc*。这决定了展示结果的次序。  

比如：  
```ql
from int x, int y
where x = 3 and y in [0 .. 2]
select x, y, x * y as product, "product: " + product
```

这个查询自己返回了下列结果： 

|x|	y|	product||
|-|-|-|-|
|3|	0|	0|	product: 0|
|3|	1|	3|	product: 3|
|3|	2|	6|	product: 6|

你也可以在select子句末尾添加 *order by y desc*。现在结果依据 *y* 列的值变得有序。  
|x|y|product| |
|-|-|-|-|
|3|2|6|product:6|
|3|1|3|product:3|
|3|0|0|product:0|  

## 查询谓词(Query predicates)
一个查询谓词是一个带有 *query* 注解的非成员谓词。它返回了谓词求解的所有元组。  
比如：  
```ql
query int getProduct(int x, int y) {
  x = 3 and
  y in [0 .. 2] and
  result = x * y
}
```
这个谓词返回下列结果：  
|x|y|result|
|-|-|-|
|3|0|0|
|3|1|3|
|3|2|6|
写一个查询谓词而不是select子句的好处是你可以在代码的其他部分调用该谓词。比如，你可以在类的主体中调用*getProduct*：  
```ql
class MultipleOfThree extends int {
  MultipleOfThree() { this = getProduct(_, _) }
}
```
与此相反，select子句像是一个匿名谓词，因此你不能在之后调用它。  

当你调试代码时，给一个谓词添加 *query* 注解是非常有用的。这样你可以显式地查看谓词计算出来的元组集。  