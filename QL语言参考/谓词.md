# 谓词(Predicates)
谓词用于描述逻辑关系，来组成QL程序。  
严格来讲，一个谓词等于一个元组集合。比如，考虑下面两个谓词的定义：  
``` ql
predicate isCountry(string country) {
  country = "Germany"
  or
  country = "Belgium"
  or
  country = "France"
}

predicate hasCapital(string country, string capital) {
  country = "Belgium" and capital = "Brussels"
  or
  country = "Germany" and capital = "Berlin"
  or
  country = "France" and capital = "Paris"
}
```
谓词*isCountry*是一元组的集合 *{("Belgium"),("Germany"),("France")}*, 而*hasCapital*是二元组的集合 *{("Belgium","Brussels"),("Germany","Berlin"),("France","Paris")}*。 这两个谓词的参数数量(arity)分别是1和2.   

通常，一个谓词中的所有元组拥有相同数量的元素。谓词的参数数量是元素的数量，不包括可能值*result*。  

## 定义谓词
当定义谓词的时候，你应该指定：  
+ 关键词*predicate* (没有结果的谓词)，或者结果类型(有结果的谓词)  
+ 谓词的名称。这是一个以小写字母开头的标识符
+ 谓词的参数。用逗号隔开。对于每个参数，指定参数类型和标识符
+ 谓词体。用花括号包含的逻辑公式(logical formula)

> Note:  
> 一个抽象(*abstract*)或者外部(*external*)谓词没有谓词体。为了定义这种谓词，使用分号(;)来结束谓词的定义。  

## 没有结果的谓词(Predicates without result)
这些谓词定义以关键词*predicate*开头。如果一个值满足谓词体中的逻辑属性，则谓词拥有这个值。  

比如：  
``` ql
predicate isSmall(int i) {
  i in [1 .. 9]
}
```
如果*i*是一个整型，那么*isSmall(i)* 包含小于10的正数。  

## 有结果的谓词(Predicates with result)
你可以通过将关键词*predicate*替换成结果的类型来定义带结果的谓词。这引入了一个特殊的变量*result*.  

比如：  
``` ql
int getSuccessor(int i) {
  result = i + 1 and
  i in [1 .. 9]
}
```  
如果*i*是小于10的正整数，则谓词的结果是*i*的后继。  

你可以将*结果*作为谓词的任一个参数。你可以表示*结果*与其他变量的关系。比如，给定谓词*getAParentOf(Person x)*， 返回*x*的父母，你可以定义一个“反转”谓词如下：  
``` ql
Person getAChildOf(Person p) {
  p = getAParentOf(result)
}
```  

一个谓词对于它参数的每个值拥有多个结果也是可能的。比如：  
``` ql
string getANeighbor(string country) {
  country = "France" and result = "Belgium"
  or
  country = "France" and result = "Germany"
  or
  country = "Germany" and result = "Austria"
  or
  country = "Germany" and result = "Belgium"
}
```  
在这个例子中：  
+ 谓词*getANeighbor("Germany")* 返回两个结果：*"Austria"* 和 *"Belgium"*  
+ 谓词*getANeighbor("Belgium")* 不返回结果。因为 *getANeighbor* 没有定义 *"Belgium"* 的 *结果*。  

## 递归谓词
QL中的谓词是可递归的。这意味着，它直接或间接地依赖于它自身。  

比如，你可以使用递归来改善上面的例子。照现在这个样子，*getANeighbor*中定义的关系并不是对称的——它不能捕捉到如果*x*是*y*的一个邻居，那么*y*也是*x*的一个邻居这个事实。一个简单的方法是递归地调用这个谓词，正如下面所示。  

``` ql
string getANeighbor(string country) {
  country = "France" and result = "Belgium"
  or
  country = "France" and result = "Germany"
  or
  country = "Germany" and result = "Austria"
  or
  country = "Germany" and result = "Belgium"
  or
  country = getANeighbor(result)
}
```  
现在 *getANeighbor("Belgium")* 也能返回结果，即 *"France"*，*"Germany"*  

## 谓词种类
有三种谓词，即非成员谓词(non-member predicates)，成员谓词(member predicates)，和特征谓词(characteristic predicates)。  

非成员谓词是被定义在类外，因此它们不是任意类的成员。  

下面是一个例子：  
```ql
int getSuccessor(int i) {  // 1. Non-member predicate
  result = i + 1 and
  i in [1 .. 9]
}

class FavoriteNumbers extends int {
  FavoriteNumbers() {  // 2. Characteristic predicate
    this = 1 or
    this = 4 or
    this = 9
  }

  string getName() {   // 3. Member predicate for the class `FavoriteNumbers`
    this = 1 and result = "one"
    or
    this = 4 and result = "four"
    or
    this = 9 and result = "nine"
  }
}
```
你也可以为每个谓词添加注解。  

## 绑定行为(Binding behavior)
必须能在有限时间内计算出一个谓词的值，因此它描述的集合通常不能是无限的。换句话说，一个谓词只能包含一个有限数量的元组。  

当QL编译器能够证明一个谓词包含的变量没有被限制为有限数量的值时，它会报告一个错误。 

这是关于无限谓词的一些例子：  
```ql
/*
  Compilation errors:
  ERROR: "i" is not bound to a value.
  ERROR: "result" is not bound to a value.
*/
int multiplyBy4(int i) {
  result = i * 4
}

/*
  Compilation error:
  ERROR: "str" is not bound to a value.
*/
predicate shortString(string str) {
  str.length() < 10
}
```
在 *multiplyBy4*，参数 *i* 被声明为 *int*型，是一个无限类型。它被用于二元操作符 *\**， 没有绑定它的操作数。 *result* 在最开始是无界的，并一直保持无界，因为它被用于和无界的 *i \* 4* 相等检查。  

在*shortString*中， *str* 保持着无界，因为它用一个无限类型 *string*声明，而内建函数*length()*并没有绑定它。  

## 绑定集合(Binding sets)
有时候你可能想要定义一个“无限谓词”，因为你只想要在一个受限的参数集合上使用它。在这种情况下，你能够使用 *bindingset* 注解来指定显式绑定集合。这个注解对于任何谓词类型都是可用的。  

比如：  
```ql
bindingset[i]
int multiplyBy4(int i) {
  result = i * 4
}

from int i
where i in [1 .. 10]
select multiplyBy4(i)
```
尽管*multiplyBy4* 是一个无限谓词，但上面的QL查询是合法的。 它先是使用了 *bindingset*注释来声明，如果 *i* 绑定到有限的值，那么谓词 *multiplyBy4* 也将会是有限的。然后在一个*i*约束到范围 *[1 .. 10]* 上下文中使用该谓词。  

也可以为一个谓词声明多个绑定集。可以通过添加多个绑定集合注解来实现，比如：  
```ql
bindingset[x] bindingset[y]
predicate plusOne(int x, int y) {
  x + 1 = y
}

from int x, int y
where y = 42 and plusOne(x, y)
select x, y
```
用这种方式指定的多个绑定集相互独立。上述例子意味着：  
+ 如果 *x* 有界，则 *x* 和 *y* 也是有界的。  
+ 如果 *y* 有界，则 *x* 和 *y* 也是有界的。  

因此，*bindingset[x] bindingset[y]* 表明了 *x* 或 *y* 至少有一个必须是有界的， 不同于 *bindingset[x, y]*， 后者表明了 *x* 和 *y* 都必须是有界的。  

当你想要声明一个有多个输入参数的带结果的谓词时，后者非常有用。比如，下列谓词会携带一个字符串 *str* 并将其截断为最大长度 *len* 个字符。  
```ql
bindingset[str, len]
string truncate(string str, int len) {
  if str.length() > len
  then result = str.prefix(len)
  else result = str
}
```
你可以在一个select子句中使用它，比如：  
```ql
select truncate("hello world", 5)
```

## 数据库谓词(Database predicates)
每一个你查询的数据库都包含着表达值之间关系的表。这些表(数据库谓词)像QL中其他谓词一样被对待。  

比如，如果一个数据库包含了一个人名表，你可以写 *persons(x, firstName, _, age)* 来约束 *x*， *firstName* 和 *age*成为表的第一，第二和第四列。  

唯一不同的地方时你不能在QL中定义数据库谓词。它们由底层数据库(underlying database)所定义。因此，可用的数据库谓词会根据你查询的数据库而不同。  
