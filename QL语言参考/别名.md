# 别名
别名是现有QL实体的替代名称。  

定义了别名之后，就可以使用这个新名称来引用当前模块命名空间中的实体。  

## 定义一个别名(Defining an alias)
您可以在任何模块的主体中定义别名。为此，你应该指定:  
+ 关键字 *module* 、*class* 或 *predicate* ，分别为模块、类型或非成员谓词定义别名。  
+ 别名应该是这种实体的有效名称。例如，有效的谓词别名以小写字母开头。  
+ 对QL实体的引用。这包括实体的原始名称，对于谓词，还包括谓词的参数数量。  

你也可以注解一个别名。  

注意，这些注释适用于由别名引入的名称(而不是底层QL实体本身)。例如，别名对其原本的名称可能具有不同的可见性。  

### 模块别名(Module aliases)  
使用下列语法去定义一个模块的别名：  
```ql
module ModAlias = ModuleName;
```
例如，如果你创建了一个新模块 *NewVersion* ，它是 *OldVersion* 的更新版本，你可以如下所示弃用 *OldVersion* 这个名称:  
```ql
deprecated module OldVersion = NewVersion;
```  

### 类型别名(Type alases)
使用下列语法为一个类型定义一个别名：  
```ql
class TypeAlias = TypeName;
```  
注意 *class* 只是一个关键字。你可以为任意类型定义一个别名——即，原始类型，数据库类型和用户定义类。  

例如，您可以使用别名将原始类型 *boolean* 的名称缩写为bool:  
```ql
class bool = boolean;
```  
或者，要使用定义再*OneTwo.qll* 中的模块 *M* 中定义的类 *OneTwo*，你可以创建一个别名来使用更短的名字 *OT* 代替:  
```ql
import OneTwoThreeLib

class OT = M::OneTwo;

...

from OT ot
select ot
``` 

### 谓词别名(Predicate aliases) 
使用下列语法为一个非成员谓词定义一个别名：  
```ql
predicate PredAlias = PredicateName/Arity;
```  
这对带结果或不带结果的谓词都有效。  

比如，假设你经常使用下列谓词，用于计算小于10的正整数的后继：  
```ql
int getSuccessor(int i) {
  result = i + 1 and
  i in [1 .. 9]
}
```  
您可以使用别名来缩写名称为 *succ* :
```ql
predicate succ = getSuccessor/1;
```  
一个没有结果的谓词的例子，假设你有一个谓词，它适用于任何小于10的正整数:  
```ql
predicate isSmall(int i) {
  i in [1 .. 9]
}
```  
您可以给谓词一个更有描述性的名称，如下所示:  
```ql
predicate lessThanTen = isSmall/1;
```