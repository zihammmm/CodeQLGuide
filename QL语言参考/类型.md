# 类型
QL是一种静态类型语言，因此每一个变量必须拥有一个声明类型。  

一个类型是一个值的集合。比如，类型 *int* 是整型的集合。注意到值可以属于这些集合中的多个，意味着它能够拥有多个类型。  

QL中的类型种类有原始类型，类，字符类型，类域类型，代数数据类型，类型联合体，以及数据库类型。  

## 原始类型(Primitive types)
这些类型内置在QL中，并且始终在全局名空间中可用，与正在查询的数据库无关。  
+ *boolean*：这个类型包含了值 *true* 和 *false*
+ *float*：这个类型包含了64位浮点数，比如6.28和-0.618
+ *string*：这个类型包含了16位字符的有限字符串
+ *date*：这个类型包含了日期(还有可选的时间)

QL在原始类型上定义了一系列的内置操作。可以通过在适当类型上使用分派来使用。比如，*1.toString()* 是整型常量*1*的字符串表示。  

## 类(Classes)
你可以在QL中定义你自己的类型。其中一种方法就是定义一个类。  

类提供了一种复用和组织代码的简单方法。比如，你可以：  
+ 将相关的值聚集起来
+ 在这些值上定义成员谓词
+ 定义重载成员谓词的子类

QL中的类并不是“创造”了一个新对象，它仅仅代表了一种逻辑属性。当一个值满足这种逻辑属性时，那么它就在一个特殊的类中。  

### 定义一个类(Defining a class)
为了定义一个类，你需要写：  
+ 关键词*class*
+ 类的名字。这是一个以大写字母开头的标识符。  
+ 需要扩展的类型
+ 类的主体，用花括号包含。  

比如：  
```ql
class OneTwoThree extends int {
  OneTwoThree() { // characteristic predicate
    this = 1 or this = 2 or this = 3
  }

  string getAString() { // member predicate
    result = "One, two or three: " + this.toString()
  }

  predicate isEven() { // member predicate
    this = 2
  }
}
```
这定义了一个类 *OneTwoThree*，包含了值 *1*, *2*, *3*。特征谓词捕获了“是整数1，2或3之一”的逻辑属性。  

*OneTwoThree* 继承了 *int*，也就是说，它是*int*的子类型。QL中的类必须总是继承至少一个已存在的类型。那些类型叫做该类的**基类**。类的值包含在基类型的交集中(也就是说，它们在类域类型(class domain type)中)。一个类从它的基类中继承了所有的成员谓词。  

一个类能够继承多个类型。关于更多信息，查看“多继承”。  

为了有效，一个类：  
+ 禁止继承它本身
+ 禁止继承一个不可变类(final class)
+ 禁止继承不完整类  

你也可以注解一个类。  

## 类体(Class bodies)
类体包含了：  
+ 特征谓词声明
+ 任意数量的成员谓词声明
+ 任意数量的域声明  

当你定义一个类时，该类也从它的父类继承了所有非私有成员谓词和域。你可以重载那些谓词和域来给它们一个更加特殊的定义。  

### 特征谓词(Characteristic predicates)
定义在类体里面的谓词。它们是逻辑属性，使用变量 *this* 来约束类中的可能的值。  

### 成员谓词(Member predicates)
这些谓词只适用于特定类的成员。你可以在值上面调用一个成员谓词。比如，你可以从上面的类中使用成员谓词：  
```ql
1.(OneTwoThree).getAString()
```  
这个调用会返回结果 *"One, two or three: 1"*。 

表达式 *(OneTwoThree)*是一个转换。它确保了 *1* 拥有类型 *OneTwoThree* 而不是 *int*。因此，它可以访问到成员谓词 *getAString()*。  

成员谓词特别好用，因此你能够将它们链接在一起。比如，你可以使用 *toUpperCase()*，一个为 *string* 定义的内置函数：  
```ql
1.(OneTwoThree).getAString().toUpperCase()
``` 
这个调用会返回 *"ONE, TWO OR THREE: 1"*:  

> 注意：  
> 特征谓词和成员谓词经常使用变量 *this*。这个变量总是指向类的成员——在本例中是一个属于类 *OneTwoThree*的值。在特征谓词中，变量 *this* 约束了类中的变量。在成员谓词中， *this* 的作用与谓词中任意其他参数相同。  

### 域(Fields)
这是声明在类体中的变量。类可以在它的类体中拥有任意数量的域声明(也就是说，变量声明)。你可以在类的谓词声明中使用这些变量。与变量 *this* 类似，域必须在特征谓词中被约束。  

比如：  
```ql
class SmallInt extends int {
  SmallInt() { this = [1 .. 10] }
}

class DivisibleInt extends SmallInt {
  SmallInt divisor;   // declaration of the field `divisor`
  DivisibleInt() { this % divisor = 0 }

  SmallInt getADivisor() { result = divisor }
}

from DivisibleInt i
select i, i.getADivisor()
```  
在这个例子中，声明*SmallInt divisor* 引入了域 *divisor*，并在特征谓词中约束它，然后在成员谓词 *getADivisor* 的声明中使用它。 这与通过在 *from* 部分声明变量来在select子句中引入变量类似。  

你也可以注解谓词和域。  

### 具体类(Concrete classes)
上面例子的类都是具体类。它们通过在一个更大的类型中约束值来定义。在具体类中的值是基类交集中的值，这些基类型也满足类的特征谓词。  

### 抽象类(Abstract classes)
一个类用 *abstract* 来注解，被称为抽象类，也是一个更大的类型中对值的约束。然而，一个抽象类被定义为它子类的联合体。特别地，一个值要在抽象类中，它必须满足它本身和它子类的特征谓词。  

如果你想要在一个共同的名称下聚集多个已存在的类，那么抽象类是有用的。你可以在这些类中定义成员谓词。你也可以扩展预定义的抽象类；比如，如果你引入了一个包含了抽象类的库，你可以添加它更多的子类。  

例子：  
如果你正在写一个安全查询，你可能对鉴别所有可以被解释为SQL查询语句的表达式感兴趣。你可以使用下面的抽象类来表述这些表达式：  
```ql
abstract class SqlExpr extends Expr {
  ...
}
```

现在定义各种子类——每种数据库管理系统为一个。比如，你可以定义一个子类 *class PostgresSqlExpr extends SqlExpr*，这包含了传递给一些执行数据库查询的Postgres API的表达式。你可以为MySQL和其他数据库管理系统定义相似的子类。  

抽象类 *SqlExpr* 引用了所有那些不同的表达式。如果你想要之后添加另一个数据库系统的支持，你可以简单地向 *SqlExpr* 添加一个新的子类；不需要更新依赖它的查询。  

> 重要：  
> 当你向一个已存在的抽象类添加一个新的子类时必须注意。添加子类并不是一个孤立的改变，它也会扩展该抽象类，因为它是子类的联合体。  

### 重载成员谓词(Overriding member predicates)
如果一个类从一个超类继承了一个成员谓词，你可以重载已继承的定义。你可以通过定义一个同名同参数数量的成员谓词来作为已继承的谓词，然后添加 *override*注解。当你想要细化谓词，以为子类的值提供更加具体的结果时很有用。  

比如，从第一个例子中扩展类：  
``` ql
class OneTwo extends OneTwoThree {
  OneTwo() {
    this = 1 or this = 2
  }

  override string getAString() {
    result = "One or two: " + this.toString()
  }
}
``` 
成员谓词 *getAString()* 重写了 *OneTwoThree* 中 *getAString()*的原先的定义。  

现在，考虑下列查询：  
```ql
from OneTwoThree o
select o, o.getAString()
```  
查询使用了谓词 *getAString()* "最具体的"定义，因此结果如下：  
|o|*getAString()* result|
|-|-|
|1|One or two: 1|
|2|One or two: 2|
|3|One, two or three: 3|

在QL中，不像其他面向对象语言，相同类型的不同子类型不需要互斥。比如，你可以定义 *OneTwoThree* 的另一个子类型，与 *OneTwo* 重叠：  
```ql
class TwoThree extends OneTwoThree {
  TwoThree() {
    this = 2 or this = 3
  }

  override string getAString() {
    result = "Two or three: " + this.toString()
  }
}
```
现在值2被两个类 *OneTwo* 和 *TwoThree* 所包含。它们都重写了 *getAString()* 的原有的定义。有了新的“最具体的”定义，运行上面的查询，给出了下列结果：  
|o|*getAString()* result|
|-|-|
|1|One or two: 1|
|2|One or two: 2|
|2|Two or three: 2|
|3|Two or three: 3|  

### 多继承(Multiple inheritance)
一个类能够扩展多个类。在那种情况下，它继承了所有类。  

比如，使用上面的定义：  
```ql
class Two extends OneTwo, TwoThree {}
```
类*Two*中的任意值必须满足 *OneTwo*表示的逻辑属性，以及 *TwoThree* 表示的逻辑属性。类 *Two* 包含一个值，即2.  

它从 *OneTwo* 和 *TwoThree* 继承了成员谓词。它也(间接地)从 *OneTwoThree* 和 *int* 继承了。  

> 注意：  
> 如果一个子类继承了相同谓词名的多个定义，那它必须重载那些定义来避免歧义。超表达式在这种情况下经常很有用。  

## 字符类型和类域类型(Character types and class domain types)
你不能够直接引用这些类型，但是在QL中的每一个类都隐式地定义了一个字符类型和类域类型。(这些是相当微妙的概念，在实际的查询编写中并不经常出现。)  

一个QL类的字符类型是一组满足类的特征谓词的值。它是域类型的子集。对于具体类，一个值属于类，当且仅当它在字符类型中。对于抽象类，一个值除了属于字符类型之外，还必须属于至少一个子类型。  

一个QL类的域类型是它的所有超类型的字符类型的交集，也就是说，如果一个值属于每一个超类，那么它也属于域类型。在类的特征谓词中以 *this* 类型出现。  

## 代数数据类型(Algebraic datatypes)
> 注意：  
> 代数数据类型的语法被认为是实验性的，可能会发生改变。然而，它出现在标准QL库中，因此下面的章节应该会帮助你理解那些例子。  

一个代数数据类型是用户定义类型的另一种形式，用关键词 *newtype* 声明。  

代数数据类型用于创建既不是原始类型也不是数据库中的实体的新值。一个例子是在分析通过程序的数据流时建模流节点。  

代数数据类型由许多相互不相交的分支组成，每个分支定义一个分支类型。代数数据类型本身是所有分支类型的并集。分支可以有参数和主体。对于每组满足实参类型和主体的值，将生成一个分支类型的新值。

这样做的好处是每个分支可以有不同的结构。
例如，如果你想定义一个“选项类型”，它要么持有一个值(比如调用)，要么为空，你可以这样写:

```ql
newtype OptionCall = SomeCall(Call c) or NoCall()
```  
这意味着程序中的每一个调用，都会产生一个不同的 *SomeCall* 值。它还意味着产生一个唯一的 *NoCall* 值。  

### 定义一个代数数据类型(Defining an algebraic datatype)
为了定义一个代数数据类型，使用下列一般语法：  
```ql
newtype <TypeName> = <branches>
```   
分支定义依照下列格式：  
``` ql
<BranchName>(<arguments>) { <body> }
```  
+ 类型名和分支名必须是以大写字母开头的标识符。习惯上，以 *T* 开头。  
+ 一个代数数据类型的不同分支使用 *or* 分开。
+ 一个分支的参数，如果有的话，是用逗号分开的变量声明。
+ 分支的主体是谓词主体。你可以省略分支主体，默认使用 *any()*。注意分支体是完全求值的，因此必须是有限的。为了保持良好的性能，它们应该保持较小的规模。  

比如，下列代数数据类型拥有3个分支：  
``` ql
newtype T =
  Type1(A a, B b) { body(a, b) }
  or
  Type2(C c)
  or
  Type3()
```  

### 使用代数数据类型的标准模式
代数数据类型不同于类。特别地，代数数据类型没有 *toString()* 成员谓词，因此你不能在select子句中使用它们。  

类经常被用于扩展代数数据类型(并且提供 *toString()* 谓词)，在标准QL语言库中，通常是这样做的：  
+ 定义一个类 *A* ，用于扩展代数数据类型和可选地声明抽象谓词
+ 对于每一个分支类型，定义类 *B* 来扩展 *A* 和分支类型，并且为 *A* 的任何抽象谓词提供定义。  
+ 注解代数数据类型为private，并将类保留为public。  

例如，下面来自c#的CodeQL数据流库的代码片段定义了处理受污染值或未受污染值的类。在这种情况下，*TaintType* 扩展数据库类型没有意义。它是污点分析的一部分，而不是底层程序的一部分，因此扩展一个新类型(即*TTaintType*)是有帮助的:  
```ql
private newtype TTaintType =
  TExactValue()
  or
  TTaintedValue()

/** Describes how data is tainted. */
class TaintType extends TTaintType {
  string toString() {
    this = TExactValue() and result = "exact"
    or
    this = TTaintedValue() and result = "tainted"
  }
}

/** A taint type where the data is untainted. */
class Untainted extends TaintType, TExactValue {
}

/** A taint type where the data is tainted. */
class Tainted extends TaintType, TTaintedValue {
}
```  

## 类型联合(Type unions)
类型联合是用关键字class声明的用户定义类型。语法类似于类型别名，但是在右边有两个或更多的类型表达式。  

类型联合用于创建现有代数数据类型的受限子集，通过显式地选择该数据类型的分支的子集并将它们绑定到新类型。它还支持数据库类型的类型联合。  

你可以使用类型联合为代数数据类型的分支子集指定名称。在某些情况下，在整个代数数据类型上使用类型联合可以避免谓词中的伪递归。例如，下面的结构是合法的:  
```ql
newtype InitialValueSource =
  ExplicitInitialization(VarDecl v) { exists(v.getInitializer()) } or
  ParameterPassing(Call c, int pos) { exists(c.getParameter(pos)) } or
  UnknownInitialGarbage(VarDecl v) { not exists(DefiniteInitialization di | v = target(di)) }

class DefiniteInitialization = ParameterPassing or ExplicitInitialization;

VarDecl target(DefiniteInitialization di) {
  di = ExplicitInitialization(result) or
  exists(Call c, int pos | di = ParameterPassing(c, pos) and
                            result = c.getCallee().getFormalArg(pos))
}
```

然而，在类扩展中限制InitialValueSource的类似实现是无效的。如果我们实现了DefiniteInitialization作为类扩展，它将触发InitialValueSource的类型测试。这将导致一个非法的递归初始化DefiniteInitialization -> InitialValueSource -> UnknownInitialGarbage -> DefiniteInitialization，因为UnknownInitialGarbage依赖于DefiniteInitialization：
```ql
// THIS WON'T WORK: The implicit type check for InitialValueSource involves an illegal recursion
// DefiniteInitialization -> InitialValueSource -> UnknownInitialGarbage -> ¬DefiniteInitialization!
class DefiniteInitialization extends InitialValueSource {
  DefiniteInitialization() {
    this instanceof ParameterPassing or this instanceof ExplicitInitialization
  }
  // ...
}
```

## 数据库类型(Database types)
数据库类型在数据库模式中定义。这意味着它们依赖于您正在查询的数据库，并且根据您正在分析的数据而变化。  

例如，如果您正在为一个Java项目查询一个CodeQL数据库，数据库类型可能包括 *@ifstmt* ，表示Java代码中的一个if语句，以及 *@variable* ，表示一个变量。  

## 类型兼容性(Type compatiility)  
并非所有类型都是兼容的。例如，*4 < “five”* 没有意义，因为不能比较 *int* 和*string* 。  

为了决定什么时候类型是兼容的，在QL中有许多不同的“类型宇宙”(type universes)。

QL中的宇宙是：  
+ 每种原始类型都有一个(除了 *int* 和 *float* ，它们都属于同一个“numbers”宇宙)。 
+ 每个数据库类型都有一个
+ 代数数据类型的每个分支都有一个

比如，当定义一个类时，这会有以下限制:  
+ 一个类不能扩展多个原始类型
+ 一个类不能扩展多个不同的数据库类型
+ 一个类不能扩展代数数据类型的多个不同分支
